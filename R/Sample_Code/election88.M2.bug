# Bugs model for election88 model

model {
  for (i in 1:n){
    y[i] ~ dbin (p.bound[i], 1)
    p.bound[i] <- max(0, min(1, p[i]))
    logit(p[i]) <- Xbeta[i]
    Xbeta[i] <- b.0 + b.female*female[i] + b.black*black[i] +
      b.female.black*female[i]*black[i] +
      a.age[age[i]] + a.edu[edu[i]] + a.age.edu[age[i],edu[i]] +
      a.state[state[i]]
  }
  b.0 ~ dnorm (0, .0001)
  b.female ~ dnorm (0, .0001)
  b.black ~ dnorm (0, .0001)
  b.female.black ~ dnorm (0, .0001)

  for (j in 1:n.age) {a.age[j] ~ dnorm(0, tau.age)}
  for (j in 1:n.edu) {a.edu[j] ~ dnorm(0, tau.edu)}
  for (j in 1:n.age) {for (k in 1:n.edu){
    a.age.edu[j,k] ~ dnorm(0, tau.age.edu)}}
  for (j in 1:n.state) {
    a.state[j] ~ dnorm(a.state.hat[j], tau.state)
    a.state.hat[j] <- a.region[region[j]] + b.v.prev*v.prev[j]}
  b.v.prev ~ dnorm (0, .0001) 
  for (j in 1:n.region) {a.region[j] ~ dnorm(0, tau.region)}

  tau.age <- pow(sigma.age, -2)
  tau.edu <- pow(sigma.edu, -2)
  tau.age.edu <- pow(sigma.age.edu, -2)
  tau.state <- pow(sigma.state, -2)
  tau.region <- pow(sigma.region, -2)

  sigma.age ~ dunif (0, 100)
  sigma.edu ~ dunif (0, 100)
  sigma.age.edu ~ dunif (0, 100)
  sigma.state ~ dunif (0, 100)
  sigma.region ~ dunif (0, 100)
}
